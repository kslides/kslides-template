plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.6.20-RC2'
    id "com.github.ben-manes.versions" version '0.42.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

// These apply if you are hosting your slides via HTTP
// Change mainName to the name of Kotlin file that has the presentation you want to serve
def mainName = "SlidesKt"

application {
    mainClassName = mainName
}

// Change this to your group name
group 'com.github.username'
version '1.0.0'

repositories {
    maven { url "https://maven.pkg.jetbrains.space/kotlin/p/kotlin/kotlin-js-wrappers" }
    maven { url "https://maven.pkg.jetbrains.space/public/p/ktor/eap" }
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    implementation "com.github.pambrose.kslides:kslides-core:$kslides_version"

    implementation "io.ktor:ktor-server:$ktor_version"
    implementation "io.ktor:ktor-server-cio:$ktor_version"
    implementation "io.ktor:ktor-server-sessions:$ktor_version"
    implementation "io.ktor:ktor-server-html-builder:$ktor_version"

    implementation "org.jetbrains.kotlin-wrappers:kotlin-css:$css_version"

    implementation "com.github.pambrose.common-utils:core-utils:$utils_version"
    implementation "com.github.pambrose.common-utils:ktor-server-utils:$utils_version"

    implementation "io.github.microutils:kotlin-logging:$logging_version"
    implementation "ch.qos.logback:logback-classic:$logback_version"
}

task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

shadowJar {
    zip64 true
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude "LICENSE*"
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    zip64 true
    archiveFileName = 'kslides.jar'
    manifest {
        attributes('Implementation-Title': 'kslides')
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}

compileKotlin {
    kotlinOptions {
        useIR = true
        jvmTarget = '1.8'
        freeCompilerArgs += ['-Xbackend-threads=8']
    }
}

compileTestKotlin {
    kotlinOptions {
        useIR = true
        jvmTarget = '1.8'
        freeCompilerArgs += ['-Xbackend-threads=8']
    }
}