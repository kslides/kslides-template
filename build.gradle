plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '2.1.0'
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// mainName is relevant only if you are hosting your slides via HTTP
// Change mainName to the name of Kotlin file that has the presentation you want to serve
def mainName = "SlidesKt"

application {
    mainClassName = mainName
}

// Change this to your group name
group 'com.github.username'
version '1.32.0'

repositories {
    google()
    mavenCentral()
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.kotlin.link' }
}

dependencies {
    implementation "com.github.kslides.kslides:kslides-core:$kslides_version"

    // Include this dependency if you use plotly.kt
    // implementation "com.github.kslides.kslides:kslides-plotly:$kslides_version"
}

task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

kotlin {
    jvmToolchain(17)
}

shadowJar {
    zip64 true
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude "LICENSE*"
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    zip64 true
    archiveFileName = 'kslides.jar'
    manifest {
        attributes('Implementation-Title': 'kslides')
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}
