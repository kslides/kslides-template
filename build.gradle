plugins {
    id 'java'
    id 'application'
    id 'org.jetbrains.kotlin.jvm' version '1.7.21'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

// mainName is relevant only if you are hosting your slides via HTTP
// Change mainName to the name of Kotlin file that has the presentation you want to serve
def mainName = "SlidesKt"

application {
    mainClassName = mainName
}

// Change this to your group name
group 'com.github.username'
version '1.6.0'

repositories {
    google()
    mavenCentral()
    // maven { url = 'https://maven.pkg.jetbrains.space/mipt-npm/p/sci/maven' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation "com.github.kslides.kslides:kslides-core:$kslides_version"
    implementation "io.ktor:ktor-server-html-builder:$ktor_version"
    implementation "org.jetbrains.kotlin-wrappers:kotlin-css:$css_version"

    implementation "com.github.pambrose:srcref:$srcref_version"

    // Include these dependencies if you use plotly.kt
    // implementation "com.github.kslides.kslides:kslides-plotly:$kslides_version"
    // implementation "space.kscience:plotlykt-core:$plotly_version"
}

task stage(dependsOn: ['uberjar', 'build', 'clean'])
build.mustRunAfter clean

shadowJar {
    zip64 true
    mergeServiceFiles()
    exclude "META-INF/*.SF"
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
    exclude "LICENSE*"
}

task uberjar(type: Jar, dependsOn: shadowJar) {
    zip64 true
    archiveFileName = 'kslides.jar'
    manifest {
        attributes('Implementation-Title': 'kslides')
        attributes('Implementation-Version': version)
        attributes('Built-Date': new Date())
        attributes('Built-JDK': System.getProperty('java.version'))
        attributes('Main-Class': mainName)
    }
    from zipTree(shadowJar.archiveFile)
}

compileKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = '11'
    }
}